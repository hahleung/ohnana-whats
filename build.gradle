buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE')
        classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath('org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1')
    }
}

group = 'ohnana'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property 'sonar.projectName', 'ohnana-whats'
        property 'sonar.projectDescription', 'Ohnana Project - Sonar'
        property 'sonar.projectKey', 'ohnana:ohnana-whats'
        property "sonar.sources", "src"
        property "sonar.sourceEncoding", "UTF-8"
    }
}

mainClassName = 'ohnana.Application'

jar {
    baseName = 'ohnana'
    version =  '0.1.0'
}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    compile 'io.sentry:sentry-spring:1.6.3'
}

task buildDocker(type: Docker, dependsOn: build) {
    applicationName = jar.baseName
    dockerfile = file('Dockerfile')
    doFirst {
        copy {
            from jar
            into "${stageDir}/target"
        }
    }
}

task stopDocker(type: Exec) {
    workingDir '.'
    executable './scripts/stop_docker.sh'
}

task runDocker(type: Exec) {
    dependsOn clean, buildDocker
    workingDir '.'
    executable './scripts/run_docker.sh'
    finalizedBy stopDocker
}

task stopDockerCompose(type: Exec) {
    workingDir '.'
    executable './scripts/stop_docker_compose.sh'
}

task runDockerCompose(type: Exec) {
    dependsOn clean, buildDocker
    workingDir '.'
    executable './scripts/run_docker_compose.sh'
    finalizedBy stopDockerCompose
}
